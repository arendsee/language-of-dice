/*
    This script is a test of random character generators
*/

// a function for leveling a character
level ~ par(character, n=1){
    character.hp += n * character.hitdie
    character
}

init ~ sum(max(4d6, 3))

hobbit ~ {
    race = 'hobbit'
    name = {'Frodo', 'Samwise', 'Merry', 'Pippin'}
    con = init
    str = init
    dex = init + 2
    int = init
    wis = init
    chr = init
    hitdie ~ 1d8
    hp = 8 + hitdie + con
}

human ~ {
    race = 'human'
    name = {'Aragorn', 'Boromir', 'Faramir'}
    _conmod = 1d3 - 1
    con = init + _conmod
    str = init + (2 - _conmod) 
    dex = init
    int = init
    wis = init
    chr = init
    hitdie ~ *{1d8, 1d10}
    hp = 10 + hitdie + con
}

wraith ~ {
    race = 'wraith'
    con = 16
    str = 16
    dex = 12
    int = 16
    wis = 14
    chr = 18
    hp = 10d12
}

random_character ~ {hobbit, human}

character = level(random_character, 5)

character
